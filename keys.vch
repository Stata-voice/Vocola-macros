#Commands for speaking keys

# This is a separate file so it can be included in both global commands
# and other commands, and thus used in command sequences in both places

### Modified Phonetic Alphabet 
### Adapted parts from NATO alphabet, 
### part from voice code alphabet:http://voicecode.io/doc/alphabet
<key> := (arch[ing]	=a | 
	brov[ing]	=b | 
	char[ring]	=c | 
	dell[ing]	=d | 
	etch[ing]	=e | 
	fomp[ing]	=f | 
	goof[ing]	=g |
	hark[ting]	=h |
	(ice| icing)	=i |
	jinx[ing]	=j |
	koop[ing]	=k | 
	lug[ging]	=l |
	mowsh[ing] 	=m | 
	nerb[ing]	=n | 
	ork[ing]	=o |
	pooch[ing]	=p |
	quash[ing]	=q |
	rosh[ming]	=r |
	souk[ing]	=s |
	teek[ing]	=t | 
	unks[ing]	=u |  
	(verge |verging)=v |
 	womp[ing]	=w |
	trex[ing]	=x | 
	yang[ing]	=y |
	zooch[ing]	=z | 
	0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9 | 
	"!" | "@" | "#" | "$" | "%" | "^" | "&" | "*" | "(" | ")" | "`" | 
	"~" | "-" | "_" | "=" | "+" | "\" | "|" | "[" | "{" | "]" | "}" | 
	";" | ":" | "'" | '"' | "," | "<" | "." | ">" | "/" | "?" | 
	Left | Right | Up | Down | 
	SKOOSH		=" " | 
	TARP		=Tab | 
	SHOCK		=Enter | 
	page-up		=PgUp | 
	page-down	=PgDn | 
	Backspace | Del | 
	RANDALL		=Esc | 
	Home | 
	End 
	 );
	
<key> 			= {$1};
<key> Here 		= ButtonClick() {$1};

Space Bar 		= " ";
Tab Key 		= {Tab};

CHIFF 			= ButtonClick();

# NatSpeak has built-in commands for e.g. "Press Page Down"
# This allows repeating those (e.g. "Press Page Down 3")

<special> := ( Left | Right | Up | Down | space-bar=" " | tab-key=Tab | Enter
             | page-up=PgUp | page-down=PgDn | Backspace | delete=Del
             | escape=Esc
             );
<mod> := Shift | control-key=Ctrl | Alt;
<nn> := 1..99;

[Press]               <special> <nn> = {$1_$2};
[Press]             <mod> <key> <nn> = {$1+$2_$3};
[Press]       <mod> <mod> <key> <nn> = {$1+$2+$3_$4};
[Press] <mod> <mod> <mod> <key> <nn> = {$1+$2+$3+$4_$5};
